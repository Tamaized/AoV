import net.minecraftforge.gradle.common.util.RunConfig

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

repositories {
    maven {
        url = "https://dvs1.progwml6.com/files/maven/" // JEI
    }
    /*maven {
        url = "https://modmaven.k-4u.nl/" // JEI Backup
    }*/
}

version = "1.15-${modVersion}${modVersionValue}"
group = "tamaized.aov"
archivesBaseName = "AoV"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

class DefaultRunConfig extends RunConfig {

    DefaultRunConfig(Project project, SourceSet sourceSet, boolean client) {
        super(project, client ? 'client' : 'server')
        this.taskName "Minecraft_" + name.substring(0, 1).toUpperCase(Locale.US).concat(name.substring(1)) + "_1.14"
        this.properties 'forge.logging.markers': 'SCAN,AXFORM'
        this.properties 'forge.logging.console.level': 'debug'
        this.workingDirectory project.file('run').canonicalPath
        this.source sourceSet
        this.environment 'TMP': './tmp/'
        this.ideaModule project.getName()
        this.singleInstance true
    }

    DefaultRunConfig setArgs(String... args) {
        setArgs(new ArrayList() {
            {
                for (String arg : args)
                    add(arg)
            }
        })
        return this
    }
}

minecraft {
    mappings("${project.mappingType}", "${project.mappingVersion}")
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    getRuns().addAll(
            new DefaultRunConfig(project, sourceSets.main, true).setArgs(

                    '--username', 'Tamaized',

                    '--uuid', '16fea09e-314e-4955-88c2-6b552ecf314a'

            ),

            new DefaultRunConfig(project, sourceSets.main, false)
    )
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraftVersion}-${project.forgeVersion}"

    //runtime fg.deobf("mezz.jei:jei-1.14.4:${project.jeiVersion}")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand([
                'version': "${project.version}"
        ])
    }
}

jar {
    manifest {
        attributes(["Specification-Title"     : "Angel of Vengeance",
                    "Specification-Vendor"    : "Tamaized",
                    "Specification-Version"   : "${modVersion}${modVersionValue}",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : "Tamaized",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task finalize << {
    ant.propertyfile(file: "gradle.properties") {
        entry(key: "modVersionValue", value: Integer.parseInt(project.modVersionValue) + 1)
    }
}

build.finalizedBy(finalize)
